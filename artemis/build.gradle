plugins {
    id "de.undercouch.download" version "3.1.1"
}

ext {
    home = 'opt/apache-artemis-2.3.0'
}

import de.undercouch.gradle.tasks.download.Download

task fetchDependencies(type: Download) {
    onlyIf {
        def result = exec {
            executable "ls"
            args "${buildDir}/apache-artemis-bin.tar.gz"
            ignoreExitValue = true
        }
        result.exitValue != 0
    }
    src 'http://archive.apache.org/dist/activemq/activemq-artemis/2.3.0/apache-artemis-2.3.0-bin.tar.gz'
    dest new File(buildDir, 'apache-artemis-bin.tar.gz')
}

task buildArtemisAmqpModule {
  doLast {
    exec {
      workingDir 'activemq-artemis/artemis-protocols/artemis-amqp-protocol'
      commandLine 'mvn', '-q', 'install', '-DskipTests'
    }
  }
}

task buildArtemisBaseImage {
    doLast {
        exec {
            workingDir 'artemis-base-image'
            commandLine 'mvn', 'install'
        }
    }
}

task buildTar(type: Tar) {
    from('activemq-artemis/artemis-protocols/artemis-amqp-protocol/target/artemis-amqp-protocol-2.3.0.jar') {
        into("${home}/lib")
    }
    from('artemis-amqp-connector/build/libs/artemis-amqp-connector.jar') {
        into("${home}/lib")
    }
    from('artemis-sasl-delegation/build/libs/artemis-sasl-delegation.jar') {
        into("${home}/lib")
    }
    from('config_templates') {
        into('config_templates')
    }
    from('utils') {
        into("${home}/bin/")
    }
    from(tarTree('artemis-shutdown-hook/build/distributions/artemis-shutdown-hook.tar')) {
        into("opt/")
    }
    archiveName 'artemis-image.tar'
    destinationDir file('build')
}

task pack {
    dependsOn buildTar
    dependsOn fetchDependencies
}
